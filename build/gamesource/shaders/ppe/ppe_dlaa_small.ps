
/*
ppe_dlaa_small.ps
Directionally Localized Anti Aliasing
Обнаружение коротких граней и их размытие
Начало данный шейдер берет отсюда: https://www.gamedev.net/forums/topic/594209-dlaa/
*/

#include <../struct.h>
#include <../ps.h>

//##########################################################################

#define DLAA_MUL_VAL 0.333

half2 g_vPixelSize;

//##########################################################################

sampler2D g_sColor:register(s0);

//##########################################################################

half4 main(VSO_PP IN) : COLOR0
{
	half4 vColorCenter = tex2Dlod2(g_sColor, IN.vTexUV, 0.0);

	half4 vHorizNeg0 = tex2Dlod2(g_sColor, IN.vTexUV + half2(-1.5, 0.0) * g_vPixelSize, 0.0);
	half4 vHorizPos0 = tex2Dlod2(g_sColor, IN.vTexUV + half2( 1.5, 0.0) * g_vPixelSize, 0.0); 
	half4 vVertNeg0 = tex2Dlod2(g_sColor, IN.vTexUV + half2(0.0, -1.5) * g_vPixelSize, 0.0); 
	half4 vVertPos0 = tex2Dlod2(g_sColor, IN.vTexUV + half2(0.0, 1.5) * g_vPixelSize, 0.0);

	half4 vSumHoriz = vHorizNeg0 + vHorizPos0;
	half4 vSumVert = vVertNeg0  + vVertPos0;

	half4 vDiffToCenterH = abs(vSumHoriz - (2.0 * vColorCenter)) * 0.5;  
	half4 vDiffToCenterV = abs(vSumVert - (2.0 * vColorCenter)) * 0.5;

	half fEdgeH = dot(vDiffToCenterH.xyz, DLAA_MUL_VAL);
	half fEdgeV = dot(vDiffToCenterV.xyz, DLAA_MUL_VAL);
	
	half vEdgeDetectH = saturate((3.0 * fEdgeH) - 0.1);
	half vEdgeDetectV = saturate((3.0 * fEdgeV)  - 0.1);

	half4 vAvgH = (vSumHoriz + vColorCenter) * DLAA_MUL_VAL;
	half4 vAvgV = (vSumVert + vColorCenter) * DLAA_MUL_VAL;

	half fBlurAmountH = saturate(vEdgeDetectH / dot(vAvgH.xyz, DLAA_MUL_VAL));
	half fBlurAmountV = saturate(vEdgeDetectV / dot(vAvgV.xyz, DLAA_MUL_VAL));

	half4 vResult = lerp(vColorCenter, vAvgH, fBlurAmountH);
	vResult = lerp(vResult, vAvgV, fBlurAmountV);

	return half4(vResult.rgb, vColorCenter.a);
}